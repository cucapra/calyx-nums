?start: fpcore

fpcore: "(" "FPCore" [SYMBOL] "(" argument_list ")" property_list expr ")"

argument_list: argument*    -> list
property_list: property*    -> list

argument: SYMBOL
        | "(" "!" property* SYMBOL ")"

?expr: number
     | symbol
     | "(" SYMBOL expr+ ")"                     -> operation
     | "(" _IF expr expr expr ")"               -> conditional
     | "(" LET "(" binder* ")" expr ")"         -> let
     | "(" _BANG property_list expr ")"         -> annotation

binder: "[" SYMBOL expr "]"

number: RATIONAL | DECNUM | HEXNUM
symbol: SYMBOL

property: EXPR_PROP expr
        | PROPERTY data

?data: symbol
     | number
     | ESCAPED_STRING
     | "(" data* ")"    -> list

RATIONAL: /[+-]?[0-9]+\/0*[1-9][0-9]*/
DECNUM: /[+-]?([0-9]+(\.[0-9]+)?|\.[0-9]+)(e[+-]?[0-9]+)?/i
HEXNUM: /[+-]?0x([0-9a-f]+(\.[0-9a-f]+)?|\.[0-9a-f]+)(p[+-]?[0-9]+)?/i

SYMBOL.-1: /[a-zA-Z~!@$%^&*_\-+=<>.?\/:][a-zA-Z0-9~!@$%^&*_\-+=<>.?\/:]*/
PROPERTY.-1: ":" SYMBOL

EXPR_PROP: /:(pre|spec|alt)/ END
_IF: "if" END
LET: /let\*?/ END
_BANG: "!" END

END: /(?![a-zA-Z0-9~!@$%^&*_\-+=<>.?\/:])/

COMMENT: /;.*/

%import common.ESCAPED_STRING
%import common.WS

%ignore COMMENT
%ignore WS
